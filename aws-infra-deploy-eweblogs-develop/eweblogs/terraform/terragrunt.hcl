locals {
  aws_region          = get_env("AWS_REGION")
  aws_account_id      = get_env("AWS_ACCOUNT_ID")
  env                 = get_env("ENVIRONMENT")
  service             = local.common.locals.service
  remote_identifier   = "eweblogs"
  identifier          = "infra"
  remote_state_bucket = "${local.env}-${local.remote_identifier}-plat-s3-terraform-state-${local.aws_account_id}"
  state_lock_table    = "${local.env}-${local.remote_identifier}-plat-s3-terraform-locks-${local.aws_account_id}"
  remote_state_key    = "${local.identifier}/${local.env}-${local.remote_identifier}.tfstate"
  common              = read_terragrunt_config("terragrunt/common.terragrunt.hcl")
  environment         = read_terragrunt_config("terragrunt/dev/environment.terragrunt.hcl")
}

# Terraform config generated by Terragrunt
generate "provider" {
  path      = "global_config.tf"
  if_exists = "overwrite_terragrunt"
  contents  = <<EOF
terraform {
  required_providers {
    aws = {
      source  = "hashicorp/aws"
      version = "~> 4.0"
    }

    github = {
      source  = "integrations/github"
      version = "~> 4.0"
    }
  }
}

provider "aws" {
  region              = "${local.aws_region}"
  allowed_account_ids = [
    "${local.aws_account_id}",
  ]
  default_tags {
    tags = {
      environment      = "${local.env}"
      service          = "${local.common.locals.service}"
      sub_service      = "${local.common.locals.sub_service}"
      owner            = "${local.common.locals.owner}"
      business_unit    = "${local.common.locals.business_unit}"
      product          = "${local.common.locals.product}"
      service_location = "${local.common.locals.service_location}"
      account_state    = "${local.common.locals.account_state}"
      programme_name   = "${local.common.locals.programme_name}"
      project_name     = "${local.common.locals.project_name}"
      project_code     = "${local.common.locals.project_code}"
      iac_source       = "terraform"
      dr               = "${local.common.locals.dr}"
    }
  }
}

provider "github" {}

EOF
}

remote_state {
  backend = "s3"
  generate = {
    path      = "backend.tf"
    if_exists = "overwrite"
  }
  config = {
    bucket         = "${local.remote_state_bucket}"
    key            = "${local.remote_state_key}"
    region         = "${local.aws_region}"
    encrypt        = true
    dynamodb_table = "${local.state_lock_table}"
  }
}

inputs = merge(
  local.common.locals,
  local.environment.locals
)